plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    alias libs.plugins.compose.compiler
    id 'jacoco'
}

android {
    compileSdk 36

    defaultConfig {
        applicationId "com.klee.sapio"
        minSdk 21
        targetSdk 35
        versionCode 67
        versionName "1.12.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildFeatures {
        viewBinding = true
        compose = true
    }
    namespace 'com.klee.sapio'
}

jacoco {
    toolVersion = "0.8.13"
    reportsDirectory = layout.buildDirectory.dir("jacocoReports")
}

// Ensure jacoco agent instruments all JVM unit tests (including Robolectric).
tasks.withType(Test).configureEach {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes += ['jdk.internal.*']
}

tasks.register("jacocoTestReport", JacocoReport) {
    dependsOn tasks.test

    reports {
        xml.required = true
        html.required = true
    }

    def fileFilter = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            '**/android/**/*.*',
            '**/androidTest/**/*.*',
            '**/test/**/*.*',
            '**/*$ViewInjector*.*',
            '**/*$ViewBinder*.*',
            '**/databinding/**/*.*'
    ]

    // Kotlin and Java classes live in different intermediate folders with AGP 8+.
    def kotlinDebugTree = fileTree(
            dir: "${project.buildDir}/tmp/kotlin-classes/debug",
            excludes: fileFilter
    )
    def javaDebugTree = fileTree(
            dir: "${project.buildDir}/intermediates/javac/debug/compileDebugJavaWithJavac/classes",
            excludes: fileFilter
    )
    def mainSrc = [
            "${project.projectDir}/src/main/java",
            "${project.projectDir}/src/main/kotlin"
    ]

    sourceDirectories.from = files(mainSrc)
    classDirectories.from = files([kotlinDebugTree, javaDebugTree])
    executionData.from = fileTree(dir: "${project.buildDir}", includes: ["**/*.exec", "**/*.ec"])
}

tasks.register("jacocoTestCoverageVerification", JacocoCoverageVerification) {
    dependsOn tasks.jacocoTestReport

    violationRules {
        rule {
            limit {
                minimum = 0.5 // 50% coverage minimum
            }
        }
    }
}

dependencies {

    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.constraintlayout

    implementation libs.glide
    implementation libs.coil.compose
    implementation libs.coil.network.okhttp

    implementation libs.kotlinx.coroutines.android
    implementation libs.androidx.navigation.fragment.ktx
    implementation libs.androidx.navigation.ui.ktx
    implementation libs.androidx.recyclerview
    implementation libs.room.runtime
    implementation libs.room.ktx
    kapt libs.room.compiler

    // hilt
    implementation libs.hilt.android
    implementation libs.androidx.runner
    implementation libs.androidx.material3.android
    kapt libs.hilt.compiler
    testImplementation libs.hilt.android.testing

    implementation libs.androidx.emoji2
    implementation libs.androidx.emoji2.views
    implementation libs.androidx.emoji2.views.helper

    implementation libs.rootbeer.lib

    testImplementation libs.junit
    testImplementation libs.mockito.core
    testImplementation libs.mockito.inline
    testImplementation libs.robolectric
    testImplementation libs.kotlinx.coroutines.test
    testImplementation libs.androidx.arch.core.testing

    androidTestImplementation libs.mockito.android
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core

    // retrofit
    implementation libs.retrofit
    implementation libs.converter.jackson
    implementation libs.logging.interceptor
    implementation libs.retrofit2.kotlin.coroutines.adapter

    implementation libs.circleimageview

    implementation libs.androidx.swiperefreshlayout

    // splashscreen
    implementation libs.androidx.core.splashscreen
    // ktor
    implementation libs.ktor.client.core
    implementation libs.ktor.client.android

    // preferences
    implementation libs.androidx.preference.ktx

    // compose
    implementation platform(libs.androidx.compose.bom)
    implementation(libs.androidx.foundation)
    implementation libs.androidx.ui
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.activity.compose
    debugImplementation libs.androidx.ui.tooling
}
